class Main {
	function void main() {
		var Game game;
		var int seed;
		var boolean playing;
		var char c;

		let playing = true;

		do Game.init();

		while (playing) {
			do Screen.clearScreen();
			do Output.moveCursor(10, 18);
			do Output.printString("Press enter to begin playing!");
			let c = 0;

			while (c = 0) {
				let c = Keyboard.keyPressed();
				let seed = seed + 1;
			}
			if ((c = 128) & (game = null)) {
				let game = Game.new(seed);
			}

			if (c = 128) {
				do game.reset();
				while (~Game.isFinished()) {
					do game.tick();
					// Wait
					do Sys.wait(17);
				}
				// Force any keys to be released
				do Keyboard.readChar();
			}
			else {
				let playing = false;
			}
		}

		if (~(game = null)) {
			do game.dispose();
		}

		return;
	}
}
