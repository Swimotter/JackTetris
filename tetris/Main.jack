class Main {
	function void main() {
		var Game game;
		var int seed;
		var boolean playing;
		var char c;
		// Time in milliseconds
		var int frameTime;
		do MemoryTracker.init();

		let playing = true;
		// 1/60th of a second
		let frameTime = 17;

		do Game.init();

		let game = null;

		while (playing) {
			do Screen.clearScreen();
			do Output.moveCursor(10, 18);
			do Output.printString("Press enter to begin playing!");
			let c = 0;

			while (c = 0) {
				let c = Keyboard.keyPressed();
				let seed = seed + 1000;
			}

			if (c = 128) {
				if (game = null) {
					let game = Game.new(seed);
				} else {
					do game.reset();
				}

				// Game loop
				while (~Game.isFinished()) {
					do game.tick();
					do Sys.wait(frameTime);
				}
				// Force any keys to be released
				do Keyboard.readChar();
			}
			else {
				let playing = false;
			}
		}

		if (~(game = null)) {
			do game.dispose();
		}

		return;
	}
}
